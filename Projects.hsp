/* ---------------------------------------------------------
	FloatPort ver. 1.1.0
	Floating Browser for Windows

	2025 ABATBeliever. All rights reserved.

	This Software is in Under MIT LICENSE.
   --------------------------------------------------------- */
#define ApplicationName    "FloatPort"
#define ApplicationVersion "1.1.0"
#define ApplicationUpdateV 110
#define RequireHSPVersion  14092 //HSP 3.7 Stable

/* ---------------------------------------------------------
	Build Config
   --------------------------------------------------------- */
#packopt version "src/Build.ini"
#packopt icon    "src/FloatPort.ico"
#packopt hide 1

/* ---------------------------------------------------------
	Include / Windows API Load
   --------------------------------------------------------- */
#epackdir "include/*"

#include "mod_wv2s.hsp"
#include "hspext.as"
#include "lib/mod_openini.as"
#include "lib/mod_mdispinfo.as"

#include "user32.as"
#include "hspinet.as"

#uselib "kernel32"

#func    SetProcessDPIAware "SetProcessDPIAware"
#func    GetModuleMyName    "GetModuleFileNameA"  int,  var,  int

/* ---------------------------------------------------------
	Define Constance
   --------------------------------------------------------- */
#define global WV2Ctrl_get_CoreWebView2      25
#define global WV2_Navigate                   5
#define global WV2_get_CanGoBack             38
#define global WV2_get_CanGoForward          39
#define global WV2_get_GoBack                40
#define global WV2_get_GoForward             41
#define global WV2_add_DocumentTitleChanged  46
#define global WV2_del_DocumentTitleChanged  47
#define global WV2_get_DocumentTitle         48

if RequireHSPVersion>hspver { dialog "D-Version is outdated.",1,"Error 0x90" }

/* ---------------------------------------------------------
	init
   --------------------------------------------------------- */
TriggerReboot = 0
TriggerReset  = 0

// リセットフラグがある場合WebView2セッションを消去
if dir_cmdline = "Reset" {
	exist "hspext.dll"
	if strsize=-1 { 
		dialog "hspext.dll が存在しないため、リセット処理は実施できませんでした。",1,"Error 0x02"
	}else{
		sdim res,65535
		GetModuleMyName 0,res,65535
		Before = res+".WebView2"    //Year     Month          Day             Hour            Min         Sec
		After  = Before+"_old_"+gettime(0)+"."+gettime(1)+"."+gettime(3)+"."+gettime(4)+"."+gettime(5)+"."+gettime(6)
		tmp ="本当にデータをリセットしますか?\n\n続行すると、\n・閲覧履歴\n・Cookie\nがバックアップの上削除され、設定は維持されます。"
		tmp+="\n\nBefore:\n"+Before+"\nAfter:\n"+After+""
		dialog tmp,2,"確認"
		await 1000
		if stat=6 {
			fxren Before,after
			if stat=0 {
				dialog "リセットしました。",0
			}else{
				dialog "リセットに失敗しました。\n\nほかに、アクティブな"+ApplicationName+"が活動していませんか?",1,"Error 0x03"
			}
		}
	}
}

/* ---------------------------------------------------------
	Load Settings
   --------------------------------------------------------- */
exist "config.ini"
if strsize=-1 { bcopy "include/default.ini","config.ini" } // 無い場合デフォルトを書き出し

SetINI_File dir_cur+"\\config.ini",2048

// int

IsActivate       = 1
SysMode          = getini_int("General","SysMode"          , 0)
IsForceFlow      = getini_int("General","IsForceFlow"      , 0)
LayerLevel       = getini_int("General","LayerLevel"       ,20)
SearchEngineList = getini_int("General","SearchEngineList" , 0)

//文字列、「,」連結を配列のintに代入
sdim tmp
tmp = getini_str("General","ExecuteKey","16,17")
split tmp,",",tmp
dim ExecuteKey,length(tmp)
repeat length(tmp)
	ExecuteKey(cnt)=int(tmp(cnt))
loop

//文字列、「https://」「http://」を削除して文字型に代入
sdim tmp
tmp = getini_str("General","FirstPage","www.google.com")
if instr(tmp,0,"http://")!=-1 or instr(tmp,0,"https://")!=-1 {
	split tmp,"://",tmp
	FirstPage=tmp(1)
}else{
	FirstPage=tmp
}

/* ---------------------------------------------------------
	Module
   --------------------------------------------------------- */
#module
	#defcfunc Shortcut_NumToText array tmp
		if length(tmp)>3{
			dialog "ショートカットキーの登録が無効です。",1,"Error 0x12"
			end
		}
		res=""
		repeat length(tmp)
			switch tmp(cnt)
			case  9 : res+="TAB"		: swbreak
			case 16 : res+="Shift"		: swbreak
			case 17 : res+="Ctrl"		: swbreak
			case 18 : res+="ALT"		: swbreak
			case 27 : res+="ESC"		: swbreak
			case 32 : res+="Space"		: swbreak
			case 33 : res+="PageUP"		: swbreak
			case 34 : res+="PageDown"	: swbreak
			case 35 : res+="END"		: swbreak
			case 36 : res+="HOME"		: swbreak
			default
				if       tmp(cnt)>47  and tmp(cnt)< 58 {//  48〜 57 は 0〜9
					res+=str(tmp(cnt)-48)      : swbreak
				}else:if tmp(cnt)>111 and tmp(cnt)<122 {// 112〜121 は Fn
					res+="F"+str(tmp(cnt)-111) : swbreak
				}else:if tmp(cnt)> 64 and tmp(cnt)< 91 {//  65〜 90 は アルファベット
					res+=strf("%c",tmp(cnt))
				}else{									//  不明
					res+="？("+tmp(cnt)+")"
				}
			swend
			if cnt+1=length(tmp) : break
			res+=", "
		loop
	return res
#global

/* ---------------------------------------------------------
	Update Check
   --------------------------------------------------------- */
IsLatest=1
exist "hspinet.dll"
if strsize!=-1 {
	netinit
	if stat {
		dialog "hspinetの初期化に失敗しました。\n更新確認はできませんが、ソフトは使用可能です。",1,"Error 0x20"
	}else{
		neturl "https://abatbeliever.net/app/FloatPort/upd.txt"
		netrequest_get ""
		dim res
		sdim buf
		repeat
			netexec res
			if res > 0 {
				sdim buf,netgetv_size()
				netgetv_data buf
				split buf,",",buf
				if length(buf)!=3               : dialog "更新確認に失敗しました。(無効な応答)\n更新確認はできませんが、ソフトは使用可能です。",1,"Error 0x21"        : break
				if buf(0)      != "[FloatPort Update]" : dialog "更新確認に失敗しました。(無効な応答)\n更新確認はできませんが、ソフトは使用可能です。",1,"Error 0x22" : break
				if int(buf(1))  < ApplicationUpdateV   : dialog "これは開発版です。",0,"info"                                                                         : break
				strrep buf(2),"<br>","\n"
				if int(buf(1))  > ApplicationUpdateV   : dialog "新しいバージョンが公開されました！\n"+buf(2),0,"更新のお知らせ" : IsLatest=0 : break
				break
			}
			if res < 0 {
				dialog "更新確認に失敗しました。(接続失敗)\n更新確認はできませんが、ソフトは使用可能です。",1,"Error 0x23"
				break
			}
			await 10
			if cnt = 200 {
				dialog "更新確認に失敗しました。(タイムアウト)\n更新確認はできませんが、ソフトは使用可能です。",1,"Error 0x24"
				break
			}
		loop
	}
}

/* ---------------------------------------------------------
	Boot Routine
   --------------------------------------------------------- */
// 高DPI対応(Windows 10 〜)
if varptr(SetProcessDPIAware) { SetProcessDPIAware }

// 画像読み込み
buffer 1,256,32,0
redraw 0
pos   0,0         : picload "include/Left.png"			,1
pos  32,0         : picload "include/Right.png"			,1
pos  64,0         : picload "include/Pin_Active.png"	,1
pos  96,0         : picload "include/Pin_Negative.png"	,1
pos 128,0         : picload "include/Config.png"		,1
color 192,192,192 : boxf 160,2,190,30
if IsForceFlow=0{	// ピンの色反映用バッファ
	pos 192,0:gcopy 1,96,0,32,32
}else{
	pos 192,0:gcopy 1,64,0,32,32
}
redraw 1

// ウィンドウ初期化
bgscr 0,640,480,8 : title ""+ApplicationName+" "+ApplicationVersion
MainWnd=hwnd
GetWindowLong hwnd, -20
SetWindowLong hwnd, -20, stat | $80000
SetLayeredWindowAttributes MainWnd, 0, 255 * (100-LayerLevel) / 100, 2

if IsForceFlow=0{ gsel 0,1 } else { gsel 0,2 }
width ,,0,0

// イベントのフラグ
onclick gosub *OnClicked
onkey   gosub *KeyPressed
onerror goto  *OnErrorJump

// WebViewの準備
exist "wv2s.dll"
if strsize=-1   { dialog "wv2s.dllが見つかりません。",1,"Error 0x10" : end }
WV2Env 0, 0
pEnv = stat
if ( pEnv == 0 ){ dialog "WebView2の展開に失敗しました。\n書き込み権限がないか、WebView2ランタイムがありません。",1,"Error 0x11" : end }
onexit *ON_EXIT

WV2Ctrl pEnv, hwnd
curCtrl = stat
prm = varptr(curView)
ComCall curCtrl, WV2Ctrl_get_CoreWebView2, prm, 1
WV2SetSize curCtrl, 0, 32, 640,480

// UI
redraw 0
sdim SearchBox,65535
font "",24
objmode 2
objsize  32,32
pos   0, 0 : objimage 1,  0,0,  0,0,  0,0 : button gosub "",  *GoBack    : idBtn_goBack    = stat
pos  32, 0 : objimage 1, 32,0, 32,0, 32,0 : button gosub "",  *GoForward : idBtn_goForward = stat
pos  64, 0 : objimage 1,192,0, 64,0,192,0 : button gosub "",  *Pin
pos  96, 0 : objimage 1,128,0,128,0,128,0 : button       "",  *Config
objsize 200,30
pos 144, 0 : input SearchBox
objsize  32,32
pos 544, 0 : objimage 1,160,0,160,0,160,0 : button gosub "?", *About
pos 576, 0                                : button gosub "_", *Minimize
pos 608, 0                                : button       "x", *ON_EXIT_TRUE
redraw 1

// 終了用フラグ
WV2HdlNew 0, hwnd, 0x9000
resRecvHdl = stat
dim token, 2
prm = resRecvHdl, varptr(token)
ComCall curView, WV2_add_DocumentTitleChanged, prm, 2
	
// 初期ページへNavigate
if IsLatest {
	WV2Navigate curCtrl, "https://"+FirstPage
}else{
	WV2Navigate curCtrl, "https://abatbeliever.net/app/FloatPort/"
}
goto *MainLoop

*MainLoop
// モードが 1 or 2 の時はループ。 そうでない場合下のstopへ
if SysMode=1{	// ショートカットのためのループ
	if length(ExecuteKey)=2 or length(ExecuteKey)=3{
		repeat
			await 10
			flag=1
			repeat length(ExecuteKey)
				getkey tmp,ExecuteKey(cnt)
				if tmp=0 : flag=0
			loop
			if flag = 1 {
				if IsActivate=1 {   // 　表示 → 非表示
					gosub *Minimize
					IsActivate=0
				}else{              // 非表示 → 　表示
					SetLayeredWindowAttributes MainWnd, 0, 0 / 100, 2
					sendmsg hwnd, $112, $F120
					repeat 100
						if cnt>(100-LayerLevel): continue
						SetLayeredWindowAttributes MainWnd, 0, 255 * cnt / 100, 2
						await 3
					loop
					IsActivate=1
				}
			}
		loop
	}else{
		dialog "ショートカットキーの登録が無効です。",1,"Error 0x12"
		end
	}
}
if Sysmode=2{ // マウスの位置監視のためのループ
	IsActivate=1
	repeat
		await 10
		if ginfo(4)<ginfo(0) and ginfo(0)<ginfo(6) and ginfo(5)<ginfo(1) and ginfo(1)<ginfo(7){
			SetLayeredWindowAttributes MainWnd, 0, 255 * (100-LayerLevel) / 100, 2
		}else{
			SetLayeredWindowAttributes MainWnd, 0, 255 * 0 / 100, 2
		}
	loop
}
// 静的な場合停止
stop

/* ---------------------------------------------------------
	WV2 Label
   --------------------------------------------------------- */
*GoBack
	prm = 0
	ComCall curView, WV2_get_GoBack, prm, 0
	return
*GoForward
	prm = 0
	ComCall curView, WV2_get_GoForward, prm, 0
	return

/* ---------------------------------------------------------
	User Event
   --------------------------------------------------------- */
*Pin
	if IsForceFlow=0{
		gsel 1 // 書き換え 　通常 → 最前面
		pos 192,0:gcopy 1,64,0,32,32
		IsForceFlow=1
		gsel 0,2
	}else{
		gsel 1 // 書き換え 最前面 → 　通常
		pos 192,0:gcopy 1,96,0,32,32
		IsForceFlow=0
		gsel 0,1
	}
return

*Config
	// 設定画面
	onclick 0
	screen 2,320,200,8
	redraw 0
	title ""+ApplicationName+" ["+ApplicationVersion+"] - 設定"
	width ,,(ginfo(0)-100),(ginfo(1))
	syscolor 4:boxf:color 0,0,0
	gsel 2,2
	objsize 150,22
	pos   2,  2 : mes "既定の検索エンジン:\n起動時のページ    :\n透明度            :"
	pos 170,  1 : combox SearchEngineList,,"Google\nMS Bing\nDuckDuckGo\nBrave Search\nEcosia\nWikipedia(日本語)\n百度(Baidu)\n常にURLとみなす"
	objsize 150,20
	pos 170, 21 : input FirstPage
	pos   0, 60 : winobj "msctls_trackbar32", "", , $50000000 | $100, 320, 30
	hTrackbar = objinfo(stat, 2)
	sendmsg hTrackbar, $405, 1, LayerLevel
	oncmd gosub *vscroll, $114
	pos 170, 41 : input LayerLevel : objenable 3,0 : objprm 3,""+LayerLevel+"%"
	pos   2, 90 : mes "動作モード        :\nショートカット    :"
	pos 170, 88 : combox SysMode,,"静的\nショートカットモード\nマウスが来た時"
	objsize 75,20
	tmp=""
	pos 170, 108 : input tmp : objenable 5,0 : objprm 5,Shortcut_NumToText(ExecuteKey)
	pos 247, 108 : button "変更",*Shortcut_Change
	tmp="静的                	: 常に指定された透明度で表示。\nショートカットモード 	: キーで表示/非表示を制御。\nマウスが来たとき	: マウスがウィンドウ上にある時表示。"
	objsize 316,71
	pos   2,128 : mesbox tmp,,,0,0
	await 100
	redraw 1
stop // ここではループしない

#include "Shortcut_Change_Manager.hsp"

*Minimize
	// 最小化
	repeat 100
		if (100-cnt)>(100-LayerLevel) : continue
		SetLayeredWindowAttributes MainWnd, 0, 255 * (100-cnt) / 100, 2
		await 3
	loop
	sendmsg hwnd, $112, $F020
	SetLayeredWindowAttributes MainWnd, 0, 255 * (100-LayerLevel) / 100, 2
return

*About
	// 画面サイズ把握
	GetDispMaxSize w,h: p=stat
	if p=0 { dialog "画面サイズの把握に失敗しました。",1,"Error 0x00" }
	SizeMax = ""+w+"x"+h
	GetMultiDispInfo 16,prm,rect: p=stat
	if p=0 { dialog "マルチディスプレイ情報の取得に失敗しました。",1,"Error 0x01" }
	DisplayNum   = 0
	DisplayNum   = prm(2)
	PrimaryIndex = prm(3)
	DisplayInfo  = ""
	repeat DisplayNum
		p=cnt*4
		x1=rect(p): y1=rect(p+1): x2=rect(p+2): y2=rect(p+3)
		w=abs(x1-x2): h=abs(y1-y2)
		DisplayInfo+= "[ID "+cnt+"] X1:"+x1+" Y1:"+y1+" X2:"+x2+" Y2:"+y2+" ("+w+"x"+h+")\n"
	loop

	tmp =""+ApplicationName+" はABATBelieverによる個人製作ソフトウェアです。"
	tmp+="\nMITライセンスの下、「現状のまま」提供されます。"
	tmp+="\n\n<関連ウェブサイト>"
	tmp+="\n公式ページ	: https://abatbeliever.net/app/FloatPort/"
	tmp+="\nリポジトリ	: https://github.com/ABATBeliever/FloatPort"
	tmp+="\n\n<依存関係>"
	tmp+="\n・mod_openini.as   - v1.0\n　ini操作モジュール by 窓月らら(Lala Madotuki)氏"
	tmp+="\n・mod_mdispinfo.as - v1.3\n　マルチディスプレイ情報取得モジュール by 窓月らら(Lala Madotuki)氏"
	tmp+="\n・wv2s.dll\n　WebView2をHSP3で呼び出すDLL by Hiro氏"
	tmp+="\n\n======================================="
	tmp+="\n最大画面解像度: "+SizeMax+" / 総ディスプレイ数: "+DisplayNum
	tmp+="\nプライマリのIDは "+PrimaryIndex+" です。\n"+DisplayInfo
	dialog tmp,0,""+ApplicationName+" v"+ApplicationVersion+""
return

/* ---------------------------------------------------------
	WinAPI Event
   --------------------------------------------------------- */
*OnClicked
	if wparam = 1 {sendmsg hwnd, $A1,2}
	SetLayeredWindowAttributes MainWnd, 0, 255 * (100-LayerLevel) / 100, 2
return

*KeyPressed
	if SearchBox="" : return
	if wparam = 13 {
		// バーの入力確定Enterかどうかの分岐
		objsel -1
		if stat = 4 : {
			stick tmp,64,1
			if tmp&64 {          // Ctrlが押されている場合URLとして解釈
				switch SearchBox // 特殊ショートカットかどうか
					case "home"
						WV2Navigate curCtrl, "https://"+FirstPage
						swbreak
					case "exit"
						goto *ON_EXIT_TRUE
						swbreak
					case "restart"
						TriggerReboot = 1 // 再起動　フラグ
						goto *ON_EXIT_TRUE
					case "reset"
						TriggerReboot = 1 // 再起動　フラグ
						TriggerReset  = 1 // リセットフラグ
						goto *ON_EXIT_TRUE
					default     // 通常の検索強制の場合
						split SearchBox,"://",tmp
						if length(tmp)=2 and tmp(0)="http " {
							WV2Navigate curCtrl, SearchBox
						}else:if length(tmp)=2 and tmp(0)="https"{
							WV2Navigate curCtrl, SearchBox
						}else{
							WV2Navigate curCtrl, "https://"+SearchBox
						}
				swend
			}else{// 通常の検索 選択肢 : Google/Bing/DuckDuckGo/Brave/Ecosia/Wikipedia.jp/百度/常にURLとみなす
				switch SearchEngineList
				case 0
					WV2Navigate curCtrl, "https://www.google.com/search?q="+SearchBox
					swbreak
				case 1
					WV2Navigate curCtrl, "https://www.bing.com/search?q="+SearchBox+"&form=QBLH"
					swbreak
				case 2
					WV2Navigate curCtrl, "https://duckduckgo.com/?q="+SearchBox+"&ia=web"
					swbreak
				case 3
					WV2Navigate curCtrl, "https://search.brave.com/search?q="+SearchBox
					swbreak
				case 4
					WV2Navigate curCtrl, "https://www.ecosia.org/search?method=index&q="+SearchBox
					swbreak
				case 5
					WV2Navigate curCtrl, "https://ja.wikipedia.org/wiki/"+SearchBox
					swbreak
				case 6
					WV2Navigate curCtrl, "https://www.baidu.com/s?wd="+SearchBox
					swbreak
				default // 「常にURLとみなす」によるURLとしての解釈
					split SearchBox,"://",tmp
					if length(tmp)=2 and tmp(0)="http " {
						WV2Navigate curCtrl, SearchBox
					}else:if length(tmp)=2 and tmp(0)="https"{
						WV2Navigate curCtrl, SearchBox
					}else{
						WV2Navigate curCtrl, "https://"+SearchBox
					}
					swbreak
				swend
			}
			gsel 0
			objprm 4,"" // 遷移後のバーのクリア
			}
	}
return

*vscroll
	// 設定ウィンドウの透明度の項目の反映用
	if lparam = hTrackbar {
		sendmsg hTrackbar, $400
		LayerLevel = stat
		if LayerLevel>70{
			objprm 3,str(LayerLevel)+"% <Danger>"
		}else{
			objprm 3,str(LayerLevel)+"%"
		}
		gsel 0
		SetLayeredWindowAttributes MainWnd, 0, 255 * (100-LayerLevel) / 100, 2
		gsel 2
	}
return

*OnErrorJump
	// エラーの際に飛ばす番地
	dialog "特定できない重大な問題が発生しました。\n'OK'を押すと、直ちに終了されます。\n\n以下の内容とエラーの発生条件を、開発元に報告してくださると、\n今後のバージョンでの修正に役立ちます。\nlparam	:"+lparam+"\nwparam	:"+wparam+"\nVersion	:"+ApplicationVersion,1,"Error 0x91"
	end:end

*ON_EXIT
	if wparam = 2 { // 設定の保存
		if LayerLevel>70 {
			dialog "透明度が高すぎます。\n本当にこのまま続行しますか?",2,"警告"
			if stat=7{stop}
		}
		gsel 2,-1
		onclick gosub *OnClicked
		SetLayeredWindowAttributes MainWnd, 0, 255 * (100-LayerLevel) / 100, 2
		gsel 0
		goto *MainLoop
	}

	if wparam = 3 { // キーボード変更中のキャンセル
		dialog "キャンセルされました。\nショートカットキーは、「"+Shortcut_NumToText(ExecuteKey)+"」のままです。",0,"ショートカットの変更"
		onkey gosub *KeyPressed
		gsel 3,-1
		gsel 0,1 : gosub *Pin : gosub *Pin
		goto *Config
	}
goto *ON_EXIT_TRUE

*ON_EXIT_TRUE
	if ( resRecvHdl ){ // WV2セッションの終了を待つ
		prm = token(0), token(1)
		ComCall curView, WV2_del_DocumentTitleChanged, prm, 2
	}
	if curView : ComDel curView
	if curCtrl : ComDel curCtrl
	if pEnv : ComDel pEnv

	// 普通のini型A
	setini_int "General",         "SysMode",int(SysMode)
	setini_int "General",     "IsForceFlow",int(IsForceFlow)
	setini_int "General",      "LayerLevel",int(LayerLevel)
	setini_int "General","SearchEngineList",int(SearchEngineList)

	sdim tmp // ショートカットキーは,で連続される文字列
	repeat length(ExecuteKey)
		tmp+=ExecuteKey(cnt)
		if (cnt+1)=length(ExecuteKey) : break
		tmp+=","
	loop
	setini_str "General","ExecuteKey",tmp

	//URLは、「https://」,「http://」を削除する
	if instr(FirstPage,0,"://")!=-1{
		sdim tmp
		split FirstPage,"://",tmp
		setini_str "General","FirstPage",tmp(1)
	}else{
		setini_str "General","FirstPage",FirstPage
	}

	repeat 100 // 画面を透明にする
		if (100-cnt)>(100-LayerLevel) : continue
		SetLayeredWindowAttributes MainWnd, 0, 255 * (100-cnt) / 100, 2
		await 3
	loop
	
	if TriggerReboot { // 再起動フラグが有効なら自分自身を実行
		sdim res,65535
		GetModuleMyName 0,res,65535
		if TriggerReset {
			exec ""+res+" Reset" // リセットフラグがあるかどうか
		}else{
			exec ""+res
		}
	}
	end // 完全な終了